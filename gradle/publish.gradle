apply plugin: 'maven-publish'
publishing {
    publications {
        aar(MavenPublication) {
            project.tasks.publishAarPublicationToMavenLocal.dependsOn("assembleDebug")
            groupId 'com.sample.localaar'
            artifactId project.name
            version = "SNAPSHOT-1.0"
            artifact("$buildDir/outputs/aar/${project.getName()}-debug.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { addDependencyNode(dependencies,it) }
                configurations.api.allDependencies.each { addDependencyNode(dependencies,it) }
            }
        }
    }

    repositories {
        maven {
            url = "$rootDir/localMavenRepository"
        }
        project.tasks.publishAarPublicationToMavenRepository.dependsOn("assembleDebug")

    }
}

private void addDependencyNode(Node dependencies,Dependency it) {
    if (it.group == null || it.group == 'com.sample.localaar') {
        println("ignoring dependency ${it.group}:${it.name}:${it.version}")
        return
    }

    def dependency = dependencies.appendNode('dependency')
    if (it.group == rootProject.name) {
        dependency.appendNode('groupId', "com.sample.localaar")
        dependency.appendNode('version', "SNAPSHOT-1.0")
    } else {
        dependency.appendNode('groupId', it.group)
        dependency.appendNode('version', it.version)
    }
    dependency.appendNode('artifactId', it.name)
}